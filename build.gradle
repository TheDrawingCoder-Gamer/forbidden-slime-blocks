plugins {
    id 'maven-publish'
    id 'scala' 
    id 'groovy'
    id 'dev.clojurephant.clojure-base' version '0.6.0'
    id 'fabric-loom' version '0.10-SNAPSHOT'
}


archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

clojure {
    builds {
        main {
            reflection = 'warn'
            checkNamespaces = []
            aotAll()
        }
    }
}
sourceSets {
    main {
        clojure {
            classesDirectory.set(file("build/classes/clojure"))
        } 
    }
}
configurations {
    shadow 
    implementation.extendsFrom shadow 
}
dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modApi("net.fabricmc:fabric-language-scala:2.0.0+scala.3.0.2") {
	exclude group: 'net.fabricmc'
    }
    modImplementation files("../fabric-language-groovy/build/libs/fabric-language-groovy+1.1.1-fat.jar")
    compileOnly "org.apache.groovy:groovy:4.0.9"
    shadow "org.clojure:java.classpath:1.0.0"
    shadow 'org.clojure:clojure:1.11.1'  
}
processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}
tasks.named('compileScala') {
    classpath = sourceSets.main.compileClasspath 
}
tasks.named('compileGroovy') {
    classpath = sourceSets.main.compileClasspath
}
tasks.named('compileClojure') {
    classpath.setFrom(files(sourceSets.main.groovy.classesDirectory), files(sourceSets.main.scala.classesDirectory))
}
tasks.named('compileJava') {
    println(sourceSets.main.clojure.classesDirectory)
    classpath += files(sourceSets.main.scala.classesDirectory) 
    classpath += files(sourceSets.main.groovy.classesDirectory) 
    classpath += files(sourceSets.main.clojure.classesDirectory) 
}
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    configurations.shadow.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // Nothing here right now.
    }
}
